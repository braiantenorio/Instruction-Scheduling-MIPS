package main;

import java.util.List;
import java.util.ArrayList;
import java_cup.runtime.*;


parser code {:

        List Program = new ArrayList();
        List immediates = new ArrayList();
        List instructionsList = new ArrayList();
        List varDeclarationList = new ArrayList();

:}

terminal String ROPCODE; 
terminal String IOPCODE;
terminal String IMOPCODE; 
terminal String JOPCODE;
terminal String PSOPCODE; 
terminal String LALIOPCODE;
terminal String REGISTER; // se le puede poner tipos por ejemplo terminal Integer NUMBER; non terminal Integer expr, term;
terminal String IMMEDIATE; 
terminal OBRACKET; 
terminal CBRACKET;
terminal String LABEL;
terminal String LABEL_DEF;
terminal DOT_DATA;
terminal DOT_TEXT;
terminal DATA_TYPE;
terminal SYSCALL; //veamos que objeto es

non terminal List<Line> program;
non terminal List<Line> data_section;
non terminal Line var_declaration;
non terminal List<Line> var_declaration_list;
non terminal List<String> immediate_list;
non terminal List<Line> instructions_and_label_defs;
non terminal Line instruction_or_label_def;
non terminal Line instruction;

start with program;

program ::= 	data_section:d instructions_and_label_defs:i
                {: 
                        List finalProgram = new ArrayList();
                        finalProgram.add(d);
                        finalProgram.add(i);
                        RESULT = finalProgram; 
                :}
                ;

/* -------Seccion de datos------- */
data_section ::=        	DOT_DATA var_declaration_list:v DOT_TEXT
							{:      
									List<Line> data = new ArrayList();
									data.add(new Line(".data"));
									data.addAll(v);
									data.add(new Line(".text"));
									RESULT = data;
							:} 
							;

var_declaration_list ::=    var_declaration_list var_declaration:v
							{:      
									varDeclarationList.add(v);
									RESULT = varDeclarationList;
							:}
							| 
							var_declaration:v
							{:      
									varDeclarationList.add(v);
									RESULT = varDeclarationList;
							:}
							;

var_declaration ::=     LABEL_DEF:l DATA_TYPE:d immediate_list:i
                        {:      
                                String inm = "";
                                for(String s: i){
                                        inm += String.format("%s, ", s);
                                } 
                                inm = inm.substring(0, inm.length() - 2);

                                RESULT = new Line(String.format("%s %s %s", l, d, inm));
                        :}
                        ;

immediate_list ::=      IMMEDIATE:i
                        {:      
                                immediates.add(i);
                                RESULT = immediates;
                        :}
                        | 
                        immediate_list IMMEDIATE:i
                        {:      
                                immediates.add(i);
                                RESULT = immediates;
                        :}
                        ;


/* ------Instrucciones------- */

instructions_and_label_defs ::=    
                instructions_and_label_defs instruction_or_label_def:i
                {:      
                        instructionsList.add(i);
                        RESULT = instructionsList;
                :}
                |       
                instruction_or_label_def:i
                {:      
                        instructionsList.add(i);
                        RESULT = instructionsList;
                :}
                ;

instruction_or_label_def ::= 
                instruction:i
                {:      
                        RESULT = i;
                :}
                |       
                LABEL_DEF:lab
                {:      
                         RESULT = new LabelDef(String.format("%s", lab));
                :}
                ;


instruction ::= ROPCODE:opcode REGISTER:rd REGISTER:rs REGISTER:rt
                {:      
                        Instruction ins = new Instruction(String.format("%s %s, %s, %s", opcode, rd, rs, rt), opcode);
                        ins.addWriteRegister(rd);
                        ins.addReadRegister(rs);
                        ins.addReadRegister(rt);
                        RESULT = ins;
                :}
                |	    
                IOPCODE:opcode REGISTER:rt REGISTER:rs IMMEDIATE:immd
                {:      
                        Instruction ins = new Instruction(String.format("%s %s, %s, %s", opcode, rt, rs, immd), opcode);
                        ins.addWriteRegister(rt);
                        ins.addReadRegister(rs);
                        RESULT = ins;
                :}               
                |
                IMOPCODE:opcode REGISTER:rt IMMEDIATE:immd OBRACKET REGISTER:rs CBRACKET
                {:      
                        Instruction ins = new Instruction(String.format("%s %s, %s(%s)", opcode, rt, immd, rs), opcode);
                        ins.addWriteRegister(rt);
                        ins.addReadRegister(rs);
                        RESULT = ins;
                :}               
                |
                LALIOPCODE:opcode REGISTER:rd LABEL:lab
                {:      
                        Instruction ins = new Instruction(String.format("%s %s, %s", opcode, rd, lab), opcode);
                        ins.addWriteRegister(rd);
                        // aca no puse label como que lo lee, creo que esta bien xd
                        RESULT = ins;
                :}
                |
                LALIOPCODE:opcode REGISTER:rd IMMEDIATE:immd
                {:      
                        Instruction ins = new Instruction(String.format("%s %s, %s", opcode, rd, immd), opcode);
                        ins.addWriteRegister(rd);
                        RESULT = ins;
                :}
                |
                JOPCODE:opcode LABEL:lab
                {:
                        RESULT = new JumpInstruction(String.format("%s %s", opcode, lab));
                :}
                |
                JOPCODE:opcode REGISTER:rs
                {:
                        RESULT = new JumpInstruction(String.format("%s %s", opcode, rs));
                :}
                |
                PSOPCODE:opcode REGISTER:rs LABEL:lab
                {:
                        RESULT = new JumpInstruction(String.format("%s %s, %s", opcode, rs, lab));
                :}
                |
                PSOPCODE:opcode REGISTER:rs REGISTER:rt LABEL:lab
                {:
                        RESULT = new JumpInstruction(String.format("%s %s, %s, %s", opcode, rs, rt, lab));
                :}
                |
                SYSCALL:s
                {:
                        RESULT = new JumpInstruction(String.format("%s",s));
                :}
                ;
